buildscript {
    ext {
        kotlinVersion = '1.5.30'
        protobufVersion = '0.8.18'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}",
                "com.google.protobuf:protobuf-gradle-plugin:${protobufVersion}"
    }
}

allprojects {
    defaultTasks 'clean', 'build', 'jacocoTestReport'
}

subprojects {
    apply from: "$rootDir/dependencies.gradle"
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'
    apply plugin: "com.google.protobuf"

    group = 'org.suggs.grpcsandbox'

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        implementation libs.kotlin,
                libs.kotlinReflect,
                libs.slf4j,
                libs.protobuf
        implementation 'io.grpc:grpc-all:1.40.1'

        testImplementation libs.test.junitApi,
                libs.test.kotestAssertions,
                libs.test.mockito

        testRuntimeOnly libs.test.junitEngine,
                libs.logback
    }

    protobuf {
        protoc { artifact = libs.protoc }
        plugins { grpc { artifact = libs.protocGen } }
        generateProtoTasks { all()*.plugins { grpc {} } }
    }

    ext {
        sourceCompatibility = 1.16
        targetCompatibility = 1.16
    }

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
}

