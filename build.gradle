buildscript {
    ext {
        kotlinVersion = '1.5.30'
        protobufVersion = '0.8.18'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}",
                "com.google.protobuf:protobuf-gradle-plugin:${protobufVersion}"
    }
}

allprojects {
    defaultTasks 'clean', 'build', 'jacocoTestReport'
}

subprojects {
    apply from: "$rootDir/dependencies.gradle"
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'
    apply plugin: "com.google.protobuf"

    group = 'org.suggs.grpcsandbox'

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        implementation libs.kotlin,
                libs.kotlinReflect,
                libs.slf4j,
                libs.grpcAll,
                libs.grpcKotlin

        testImplementation libs.test.junitApi,
                libs.test.kotestAssertions,
                libs.test.mockito

        testRuntimeOnly libs.test.junitEngine,
                libs.logback
    }

    protobuf {
        protoc { artifact = libs.protoc }
        plugins {
            grpc { artifact = libs.protocGen }
            grpckt { artifact = libs.protoGenKt }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc {}
                grpckt {}
            }
        }
    }

    sourceSets{
        main{
            kotlin{
                srcDirs 'build/generated/sourcs/proto/main/grpc'
                srcDirs 'build/generated/sourcs/proto/main/grpckt'
                srcDirs 'build/generated/sourcs/proto/main/java'
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    java {
        sourceCompatibility = "14"
        targetCompatibility = "14"
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "14"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "14"
        }
    }
}

